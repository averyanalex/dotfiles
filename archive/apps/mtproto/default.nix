let
  name = "mtproto";
  sliceName = "apps-${name}";
  appServiceConfig = {
    Slice = "${sliceName}.slice";
    RestartMode = "direct";
    RestartSec = "5s";
    TimeoutStartSec = "4min";
  };
  appUnitConfig = {
    StartLimitIntervalSec = "10min";
    StartLimitBurst = 6;
  };
in
{ config, ... }:
{
  systemd.slices.${sliceName}.description = "MTProto application services";

  age.secrets.${name}.file = ./secret.age;

  # MTProto proxy uses raw TCP protocol
  # Nginx stream module proxies port 443 to the container

  virtualisation.quadlet =
    let
      inherit (config.virtualisation.quadlet) networks;
    in
    {
      networks.${name} = {
        networkConfig = {
          subnets = [ "10.90.94.0/24" ];
          podmanArgs = [ "--interface-name=pme-${name}" ];
        };
        serviceConfig.Slice = appServiceConfig.Slice;
      };

      containers.${name} = {
        containerConfig = {
          image = "docker.io/alexdoesh/mtproxy:latest";
          autoUpdate = "registry";
          networks = [ networks.${name}.ref ];
          ip = "10.90.94.2";
          environmentFiles = [ config.age.secrets.${name}.path ];
          gidMaps = [ "0:100000:100000" ];
          memory = "256m";
          uidMaps = [ "0:100000:100000" ];
        };
        unitConfig = appUnitConfig;
        serviceConfig = appServiceConfig;
      };
    };
}
